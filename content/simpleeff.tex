\section{Simple Effects}

\label{sect:simpleff}

So far we have seen how to write programs with locally mutable state using the
\texttt{STATE} effect. To recap, we have the definitions in
Listing \ref{eff:state} in a module \texttt{Effect.State}.

\begin{code}[float=h,frame=single,caption={State Effect}, label=eff:state]
module Effect.State

STATE : Type -> EFFECT

get    :             { [STATE x] } Eff x
put    : x ->        { [STATE x] } Eff () 
putM   : y ->        { [STATE x] ==> [STATE y] } Eff () 
update : (x -> x) -> { [STATE x] } Eff () 

instance Handler State m
\end{code}

\noindent
The last line, \texttt{instance Handler State m}, means that the \texttt{STATE}
effect is usable in any computation context \texttt{m}. That is, a program
which uses this effect and returns something of type \texttt{a} can be
evaluated to something of type \texttt{m a} using \texttt{run}, for
any \texttt{m}.
%That is, if we have a program \texttt{p : { [STATE t] } Eff a}, we can
%evaluate \texttt{run p : m a} for any \texttt{m : Type -> Type}.
%
The lower case \texttt{State} is a data type describing the operations which
make up the \texttt{STATE} effect itself---we will go into more detail about
this in Section \ref{sect:impleff}.

In this section, we will introduce some other supported effects, allowing
console I/O, exceptions, random number generation and non-deterministic
programming.
For each effect we introduce, we will begin with a summary of the effect,
its supported operations, and the contexts in which it may be used,
like that above for \texttt{STATE}, and go on to
present some simple examples. At the end, we will see some examples of programs
which combine multiple effects.

All of the effects in the library, including those described in this section,
are summarised in Appendix \ref{sect:appendix}.

\subsection{Console I/O}

\begin{code}[float=h,frame=single, caption={Console I/O Effect}, label=eff:stdio]
module Effect.StdIO

STDIO : EFFECT

putChar  : Char ->   { [STDIO] } Eff ()
putStr   : String -> { [STDIO] } Eff ()
putStrLn : String -> { [STDIO] } Eff ()

getStr   :           { [STDIO] } Eff String
getChar  :           { [STDIO] } Eff Char

instance Handler StdIO IO
instance Handler StdIO (IOExcept a)
\end{code}

\noindent
Console I/O (Listing \ref{eff:stdio})
is supported with the \texttt{STDIO} effect, which allows reading
and writing characters and strings to and from standard input and standard
output. Notice that there is a constraint here on the computation context
\texttt{m}, because it only makes sense to support console I/O operations in
a context where we can perform (or at the very least simulate) console I/O.

\subsubsection*{Examples}

A program which reads the user's name, then says hello, can be written
as follows:

\begin{code}
hello : { [STDIO] } Eff ()
hello = do putStr "Name? "
           x <- getStr
           putStrLn ("Hello " ++ trim x ++ "!")
\end{code}

\noindent
We use \texttt{trim} here to remove the trailing newline from the input.
The resource associated with \texttt{STDIO} is simply the empty tuple, which
has a default value \texttt{()}, so we can run this as follows:

\begin{code}
main : IO ()
main = run hello
\end{code}

\noindent
In \texttt{hello} we could also use \texttt{!}-notation instead of \texttt{x <-
getStr}, since we only use the string that is read once:

\begin{code}
hello : { [STDIO] } Eff ()
hello = do putStr "Name? "
           putStrLn ("Hello " ++ trim !getStr ++ "!")
\end{code}

\noindent
More interestingly, we can combine multiple effects in one program. For example,
we can loop, counting the number of people we've said hello to:

\begin{code}
hello : { [STATE Int, STDIO] } Eff ()
hello = do putStr "Name? "
           putStrLn ("Hello " ++ trim !getStr ++ "!")
           update (+1)
           putStrLn ("I've said hello to: " ++ show !get ++ " people")
           hello
\end{code}

\noindent
The list of effects given in \texttt{hello} means that the function can
call \texttt{get} and \texttt{put} on an integer state, and any functions
which read and write from the console. To run this, \texttt{main} does
not need to be changed.

\subsubsection*{Aside: Resource Types}

To find out the resource type of an effect, if necessary (for example if
we want to initialise a resource explicitiy with \texttt{runInit} rather than
using a default value with \texttt{run})
we can run the \texttt{resourceType} function at the \Idris{} REPL:

\begin{code}
*ConsoleIO> resourceType STDIO
() : Type
*ConsoleIO> resourceType (STATE Int)
Int : Type
\end{code}

\subsection{Exceptions}

Listing \ref{eff:exception} gives the definition of the \texttt{EXCEPTION}
effect, declared in module \texttt{Effect.Exception}. This allows programs
to exit immediately with an error, or errors to be handled more generally.

\begin{code}[float=h,frame=single,label=eff:exception,caption={Exception Effect}]
module Effect.Exception

EXCEPTION : Type -> EFFECT

raise : a -> { [EXCEPTION a ] } Eff b 

instance           Handler (Exception a) Maybe
instance           Handler (Exception a) List
instance           Handler (Exception a) (Either a)
instance           Handler (Exception a) (IOExcept a)
instance Show a => Handler (Exception a) IO
\end{code}

\subsubsection*{Example}

Suppose we have a \texttt{String} which is expected to represent an integer in
the range \texttt{0} to \texttt{n}. We can write a function \texttt{parseNumber}
which returns an \texttt{Int} if parsing the string returns a number in the
appropriate range, or throws an exception otherwise. Exceptions are
paramaterised by an error type:

\begin{code}
data Err = NotANumber | OutOfRange

parseNumber : Int -> String -> { [EXCEPTION Err] } Eff Int
parseNumber num str
   = if all isDigit (unpack str)
        then let x = cast str in
             if (x >=0 && x <= num)
                then pure x
                else raise OutOfRange
        else raise NotANumber
\end{code}

\noindent
Programs which support the \texttt{EXCEPTION} effect can be run in any
context which has some way of throwing errors, for example, we can run
\texttt{parseNumber} in the \texttt{Either Err} context. It returns
a value of the form \texttt{Right x} if successful:

\begin{code}
*Exception> the (Either Err Int) $ run (parseNumber 42 "20")
Right 20 : Either Err Int
\end{code}

\noindent
Or \texttt{Left e} on failure, carrying the appropriate exception:

\begin{code}
*Exception> the (Either Err Int) $ run (parseNumber 42 "50")
Left OutOfRange : Either Err Int

*Exception> the (Either Err Int) $ run (parseNumber 42 "twenty")
Left NotANumber : Either Err Int
\end{code}

\noindent
In fact, we can do a little bit better with \texttt{parseNumber}, and have it
return a \emph{proof} that the integer is in the required range along with
the integer itself. One way to do this is define a type of bounded integers,
\texttt{Bounded}:

\begin{code}
Bounded : Int -> Type
Bounded x = (n : Int ** So (n >= 0 && n <= x))
\end{code}

\noindent
Recall that \texttt{So} is parameterised by a \texttt{Bool}, and only
\texttt{So True} is inhabited. We can use \texttt{choose} to construct such
a value from the result of a dynamic check:

\begin{code}
data So : Bool -> Type = Oh : So True

choose : (b : Bool) -> Either (So b) (So (not b))
\end{code}

\noindent
We then write \texttt{parseNumber} using \texttt{choose} rather than
an \texttt{if/then/else} construct, passing the proof it returns on success
as the boundedness proof:

\begin{code}
parseNumber : (x : Int) -> String -> { [EXCEPTION Err] } Eff (Bounded x)
parseNumber x str
   = if all isDigit (unpack str)
        then let num = cast str in
             case choose (num >=0 && num <= x) of
                  Left p => pure (num ** p)
                  Right p => raise OutOfRange
        else raise NotANumber
\end{code}

%\input{catch.tex}

\subsection{Random Numbers}

Random number generation is also implemented by the \effects{} library,
in module \texttt{Effect.Random}. Listing \ref{eff:random} gives its
definition.

\begin{code}[float=h,frame=single,label=eff:random,caption={Random Number Effect}]
module Effect.Random

RND : EFFECT

srand  : Integer ->            { [RND] } Eff ()
rndInt : Integer -> Integer -> { [RND] } Eff Integer
rndFin : (k : Nat) ->          { [RND] } Eff (Fin (S k))

instance Handler Random m
\end{code}

Random number generation is considered side-effecting because its implementation
generally relies on some external source of randomness. The default 
implementation here relies on an integer \emph{seed}, which can be set with
\texttt{srand}. A specific seed will lead to a predictable, repeatable
sequence of random numbers. There are two functions which produce a random
number:

\begin{itemize}
\item \texttt{rndInt}, which returns a random integer between the given lower and upper bounds.
\item \texttt{rndFin}, which returns a random element of a finite set (essentially
a number with an upper bound given in its type).
\end{itemize}

\subsubsection*{Example}

We can use the \texttt{RND} effect to implement a simple guessing game.
The \texttt{guess} function, given a target number, will repeatedly ask the
user for a guess, and state whether the guess is too high, too low, or
correct:

\begin{code}
guess : Int -> { [STDIO] } Eff ()
\end{code}

\noindent
For reference, the code for \texttt{guess} is given in Listing \ref{eff:game}.
Note that we use \texttt{parseNumber} as defined previously 
to read user input, but we don't need to list the \texttt{EXCEPTION} effect
because we use a nested \texttt{run} to invoke \texttt{parseNumber},
independently of the calling effectful program.

To invoke these, we pick a random number within the range 0--100, having
set up the random number generator with a seed, then run \texttt{guess}:

\begin{code}
game : { [RND, STDIO] } Eff ()
game = do srand 123456789
          guess (fromInteger !(rndInt 0 100))

main : IO ()
main = run game
\end{code}

\noindent
If no seed is given, it is set to the \texttt{default} value. For a less
predictable game, some better source of randomness would be required, for
example taking an initial seed from the system time. To see how to do
this, see the \texttt{SYSTEM} effect described in Appendix \ref{sect:appendix}.

\begin{code}[frame=single,float,label=eff:game,caption={Guessing Game}]
guess : Int -> { [STDIO] } Eff ()
guess target
    = do putStr "Guess: "
         case run (parseNumber 100 (trim !getStr)) of
              Nothing => do putStrLn "Invalid input"
                            guess target
              Just (v ** _) =>
                         case compare v target of
                             LT => do putStrLn "Too low"
                                      guess target
                             EQ => putStrLn "You win!"
                             GT => do putStrLn "Too high"
                                      guess target
\end{code}

\subsection{Non-determinism}

Listing \ref{eff:select} gives the definition of the non-determinism effect,
which allows a program to choose a value non-deterministically from a list
of possibilities in such a way that the entire computation succeeds.

\begin{code}[float=h,frame=single,label=eff:select,caption={Non-determinism Effect}]
import Effect.Select

SELECT : EFFECT

select : List a -> { [SELECT] } Eff a 

instance Handler Selection Maybe
instance Handler Selection List
\end{code}

\subsubsection*{Example}

The \texttt{SELECT} effect can be used to solve constraint problems, such as
finding Pythagorean triples. The idea is to use \texttt{select} to give a
set of candidate values, then throw an exception for any combination of values
which does not satisfy the constraint:

\begin{code}
triple : Int -> { [SELECT, EXCEPTION String] } Eff (Int, Int, Int)
triple max = do z <- select [1..max]
                y <- select [1..z]
                x <- select [1..y]
                if (x * x + y * y == z * z)
                   then pure (x, y, z)
                   else raise "No triple"
\end{code}

\noindent
This program chooses a value for \texttt{z} between \texttt{1} and \texttt{max},
then values for \texttt{y} and \texttt{x}. In operation, after a
\texttt{select}, the program executes the rest of the \texttt{do}-block for
every possible assignment, effectively searching depth-first. If the list
is empty (or an exception is thrown) execution fails.

There are handlers defined for \texttt{Maybe} and \texttt{List} contexts,
i.e. contexts which can capture failure.
Depending on the context \texttt{m}, \texttt{triple} will either return the
first triple it finds (if in \texttt{Maybe} context) or all triples in the
range (if in \texttt{List} context). We can try this as follows:

\begin{code}
main : IO ()
main = do print $ the (Maybe _) $ run (triple 100)
          print $ the (List _) $ run (triple 100)
\end{code}

\subsection{\texttt{vadd} revisited}

We now return to the \texttt{vadd} program from the introduction. Recall the
definition:

\begin{code}
vadd : Vect n Int -> Vect n Int -> Vect n Int
vadd []        []        = []
vadd (x :: xs) (y :: ys) = x + y :: vadd xs ys
\end{code}

\noindent
Using \effects{}, we can set up a program so that it reads input from a user,
checks that the input is valid (i.e both vectors contain integers, and are
the same length) and if so, pass it on to \texttt{vadd}. First, we write
a wrapper for \texttt{vadd} which checks the lengths and throw an exception
if they are not equal. We can do this for input vectors of length \texttt{n}
and \texttt{m} by matching on the implicit arguments \texttt{n} and
\texttt{m} and using \texttt{decEq} to produce a proof of their equality,
if they are equal:

\begin{code}
vadd_check : Vect n Int -> Vect m Int ->
             { [EXCEPTION String] } Eff (Vect m Int)
vadd_check {n} {m} xs ys with (decEq n m)
  vadd_check {n} {m=n} xs ys | (Yes Refl) = pure (vadd xs ys)
  vadd_check {n} {m}   xs ys | (No contra) = raise "Length mismatch"
\end{code}

\noindent
To read a vector from the console, we implement a function of the following
type:

\begin{code}
read_vec : { [STDIO] } Eff (p ** Vect p Int)
\end{code}

\noindent
This returns a dependent pair of a length, and a vector of that length, because
we cannot know in advance how many integers the user is going to input.
One way to implement this function, using \texttt{-1} to indicate the end
of input, is shown in Listing \ref{readvec}. This uses a variation on
\texttt{parseNumber} which does not require a number to be within range.

Finally, we write a program which reads two vectors and prints the result
of pairwise addition of them, throwing an exception if the inputs are of
differing lengths:

\begin{code}
do_vadd : { [STDIO, EXCEPTION String] } Eff ()
do_vadd = do putStrLn "Vector 1"
             (_ ** xs) <- read_vec
             putStrLn "Vector 2"
             (_ ** ys) <- read_vec
             putStrLn (show !(vadd_check xs ys))
\end{code}

\noindent
By having explicit lengths in the type, we can be sure that \texttt{vadd} is
only being used where the lengths of inputs are guaranteed to be equal. This
does not stop us reading vectors from user input, but it does require that
the lengths are checked and any discrepancy is dealt with gracefully.

\begin{code}[float=h,frame=single,caption={Reading a vector from the console},label=readvec]
read_vec : { [STDIO] } Eff (p ** Vect p Int)
read_vec = do putStr "Number (-1 when done): "
              case run (parseNumber (trim !getStr)) of
                   Nothing => do putStrLn "Input error"
                                 read_vec
                   Just v => if (v /= -1)
                                then do (_ ** xs) <- read_vec
                                        pure (_ ** v :: xs)
                                else pure (_ ** [])
  where
    parseNumber : String -> { [EXCEPTION String] } Eff Int
    parseNumber str
      = if all (\x => isDigit x || x == '-') (unpack str)
           then pure (cast str)
           else raise "Not a number"
\end{code}

\subsection{Example: An Expression Calculator}

To show how these effects can fit together, let us consider an evaluator for
a simple expression language, with addition and integer values.

\begin{code}
data Expr = Val Integer
          | Add Expr Expr
\end{code}

\noindent
An evaluator for this language always returns an \texttt{Integer}, and there
are no situations in which it can fail!

\begin{code}
eval : Expr -> Integer
eval (Val x) = x
eval (Add l r) = eval l + eval r
\end{code}

\noindent
If we add variables, however, things get more interesting. The evaluator will
need to be able to access the values stored in variables, and variables may
be undefined.

\begin{code}
data Expr = Val Integer
          | Var String
          | Add Expr Expr
\end{code}

\noindent
To start, we will change the type of \texttt{eval} so that it is effectful,
and supports an exception effect for throwing errors, and a state containing
a mapping from variable names (as \texttt{String}s) to their values:

\begin{code}
Env : Type
Env = List (String, Integer)

eval : Expr -> { [EXCEPTION String, STATE Env] } Eff Integer
eval (Val x) = return x
eval (Add l r) = return $ !(eval l) + !(eval r)
\end{code}

\noindent
Note that we are using \texttt{!}-notation to avoid having to bind
subexpressions in a \texttt{do} block.
Next, we add a case for evaluating \texttt{Var}:

\begin{code}
eval (Var x) = case lookup x !get of
                    Nothing => raise $ "No such variable " ++ x
                    Just val => return val
\end{code}

\noindent
This retrieves the state (with \texttt{get}, supported by the \texttt{STATE Env}
effect) and raises an exception if the variable is not in the environment
(with \texttt{raise}, supported by the \texttt{EXCEPTION String} effect).

To run the evaluator on a particular expression in a particular environment
of names and their values, we can write a function which sets the state then
invokes \texttt{eval}:

\begin{code}
runEval : List (String, Integer) -> Expr -> Maybe Integer
runEval args expr = run (eval' expr)
  where eval' : Expr -> { [EXCEPTION String, STATE Env] } Eff Integer
        eval' e = do put args
                     eval e
\end{code}

We have picked \texttt{Maybe} as a computation context here; it needs to
be a context which is available for every effect supported by \texttt{eval}.
In particular, because we have exceptions, it needs to be a context which
supports exceptions. Alternatively, \texttt{Either String} or \texttt{IO}
would be fine, for example.

What if we want to extend the evaluator further, with random number generation?
To achieve this, we add a new constructor to \texttt{Expr}, which gives
a random number up to a maximum value:

\begin{code}
data Expr = Val Integer
          | Var String
          | Add Expr Expr
          | Random Integer
\end{code}

\noindent
Then, we need to deal with the new case, making sure that we extend the
list of events to include \texttt{RND}. It doen't matter where \texttt{RND}
appears in the list, as long as it is present:

\begin{code}
eval : Expr -> { [EXCEPTION String, RND, STATE Env] } Eff Integer

eval (Random upper) = rndInt 0 upper
\end{code}

\noindent
For test purposes, we might also want to print the random number which has
been generated:

\begin{code}
eval (Random upper) = do val <- rndInt 0 upper
                         putStrLn (show val)
                         return val
\end{code}

\noindent
If we try this without extending the effects list, we would see an error
something like the following:

\begin{code}
Expr.idr:28:6:When elaborating right hand side of eval:
Can't solve goal 
   SubList [STDIO]
           [(EXCEPTION String), RND, (STATE (List (String, Integer)))]
\end{code}

\noindent
In other words, the \texttt{STDIO} effect is not available. We can correct
this simply by updating the type of \texttt{eval} to include \texttt{STDIO}.

\begin{code}
eval : Expr -> { [STDIO, EXCEPTION String, RND, STATE Env] } Eff Integer
\end{code}

\noindent
Note that using \texttt{STDIO} will restrict the number of contexts 
in which \texttt{eval} can be \texttt{run} to those which support \texttt{STDIO},
such as \texttt{IO}.
Once effect lists get longer, it can be a good idea instead
to encapsulate sets of effects in a type synonym. This is achieved as follows,
simply by defining a function which computes a type, since types are first
class in \Idris{}:

\begin{code}
EvalEff : Type -> Type
EvalEff t = { [STDIO, EXCEPTION String, RND, STATE Env] } Eff t
  
eval : Expr -> EvalEff Integer
\end{code}

%FIXME: Is 'new' really a good idea?
%\subsection{Introducing effects with \texttt{new}}

%It can occasionally be useful to introduce new effects during a computation.
%For example, introducing a new explcit state
